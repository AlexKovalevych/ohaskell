----
title: О конструкторах значений
prevChapter: /ru/about-user-types/types-at-a-glance.html
nextChapter: /ru/about-user-types/type-context.html
----

Конструктор значения (value constructor) - штука полезная. В этой главе поговорим о нём подробнее.

## Иные имена

Конструктор значения может отличаться от имени самого типа. Взгляните:

```haskell
data IPAddress = IP String  -- Имя типа осталось неизменным,
                            -- но конструктор значения изменился.

instance Show IPAddress where
              |
              тип

    show (IP address) = if address == "127.0.0.1" then "localhost" else address
          |
          конструктор
          значения
```

Теперь мы будем создавать значения нашего типа так:

```haskell
main = putStrLn . show $ IP "127.0.0.1"
```

Такой подход позволяет писать более краткий, но при этом ничуть не менее понятный код.

## Множество конструкторов

Конструкторов значения может быть несколько, и эта возможность используется очень часто. Например:

```haskell
data IPAddress = IP String | Host String
                 |           |
                 Первый      Второй
                 конструктор конструктор
                 значения    значения
```

Мы ввели два конструктора, `IP` и `Host`. Здесь используется оператор `|`, знакомый программистам C как оператор бинарного "ИЛИ". Выглядит довольно логично, и поэтому мы можем прочесть эту строку так:

```haskell
data IPAddress  =    IP String  |    Host String
тип  IPAddress  это  IP String  или  Host String
```

Определим метод `show` для каждого из этих конструкторов:

```haskell
instance Show IPAddress where
    show (IP address)   = address

    show (Host address) = if address == "127.0.0.1" then "localhost" else address
```

Теперь, если мы напишем так:

```haskell
main = putStrLn . show $ IP "127.0.0.1"
```

вывод будет таким:

```bash
127.0.0.1
```

Если же мы используем второй конструктор:

```haskell
main = putStrLn . show $ Host "127.0.0.1"
```

вывод будет другим:

```haskell
localhost
```

## Множество значений

За конструктором значения может следовать как один тип, так и множество. Например, пусть наш `IPAddress` хранит не только адрес, но и порт:

```haskell
data IPAddress = Host String Int
```

Как вы уже догадались, первое значение хранит адрес, второе - порт. Тогда создание значения типа `IPAddress` будет выглядеть так:

```haskell
main = putStrLn . show $ Host "127.0.0.1" 3010
```

Взяли строку, взяли целое число, затем "применили" к ним конструктор `Host` - и в результате получилось значение типа `IPAddress`.

## О нульарных конструкторах

Нульарный конструктор (nullary constructor) используется в Haskell довольно часто. Суть его предельно проста. Вспомним наш демонстрационный тип:

```haskell
data IPAddress = IP String
```

За конструктором `IP` следует одно значение, поэтому такой конструктор называют одинарным. А нульарным называют такой конструктор, за которым никакого значения нет. Вы спросите, зачем это нужно? Вот пример.

Допустим, нам нужен тип, ассоциированный с протоколами транспортного слоя (Transport layer) модели OSI. Вот как это будет выглядеть:

```haskell
data TransportLayer = TCP | UDP | SCTP | DCCP | SPX
```

Символ `|` вы уже знаете, но значений, как видите, здесь нет. Тип `TransportLayer` имеет пять нульарных конструкторов. И чтобы продемонстрировать практическую пользу такого типа, определим функцию, описывающую конкретный протокол:

```haskell
descriptionOf :: TransportLayer -> String
descriptionOf protocol =
    case protocol of
        TCP  -> "Transmission Control Protocol"
        UDP  -> "User Datagram Protocol"
        SCTP -> "Stream Control Transmission Protocol"
        DCCP -> "Datagram Congestion Control Protocol"
        SPX  -> "Sequenced Packet Exchange"
```

Мы принимаем значение типа `TransportLayer` и возвращаем соответствующее ему строковое описание. Обратите внимание на конструкцию с ключевым словом `case`. Это - родственница конструкции `switch-case` из языка C, реализующая выбор из множества вариантов.

Теперь проверяем:

```haskell
main = print $ descriptionOf TCP
```

Для создания значения типа `TransportLayer`, к которому будет применена функция `descriptionOf`, мы использовали один из нульарных конструкторов. Никакого дополнительного значения здесь нет, потому что с концептуальной точки зрения сам нульарный конструктор и представляет собой значение. Результат будет такой:

```bash
"Transmission Control Protocol"
```

Если вам нужен тип, подразумевающий фиксированное множество именованных значений - нульарные конструкторы вам помогут.

## В сухом остатке

1. Конструктор значения (далее КЗ) используется при конструировании значении.
2. Имя КЗ может отличаться от имени самого типа.
3. КЗ может быть сколько угодно, от нуля и больше.
4. КЗ может работать как без конкретных значений (и тогда его называют нульарным), так и с любым числом значений.

